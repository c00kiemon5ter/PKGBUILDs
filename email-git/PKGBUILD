# Maintainer: c00kiemon5ter <ivan [dot] kanak [at] gmail [dot] com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Dennis Craven <dcraven @ gmail . com>

pkgname=email-git
pkgver=20130315
pkgrel=1
pkgdesc="A simple command line client for SMTP"
arch=(any)
url="http://www.cleancode.org/projects/email/"
license=('GPL')
depends=('openssl')
backup=(etc/email/{email.{address.template,conf,help,sig},mime.types})
makedepends=('git')

_gitroot="git://github.com/deanproxy/eMail.git"
_gitname="eMail"

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d $_gitname ]] ; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done"
	cd "$srcdir/$_gitname"

	msg "Fetching submodules..."
	printf '%s\n' "$ s,:,/," "$ s,@,://," "wq" | ed -s "$srcdir/$_gitname/.gitmodules" || true
	git submodule init
	git submodule update

	msg "Configuring..."
	./configure --prefix=/usr --sysconfdir=/etc

	msg "Starting make..."
	make
}

package() {
	cd "$srcdir/$_gitname"
	# make --version "3.1.3" DESTDIR="$pkgdir" install
	install -Dm755 src/email                              "$pkgdir/usr/bin/email"
	install -Dm644 email.1                                "$pkgdir/usr/share/man/man1/email.1"
	install -Dm644 mime.types                             "$pkgdir/etc/email/mime.types"
	install -cm644 email.{address.template,conf,help,sig} "$pkgdir/etc/email"
}

